
fn all @public case:
  [ping, ping_not_authenticated,
   get_session, get_session_not_authenticated,
   list_buckets, list_buckets_unauthenticated, list_buckets_unauthorized,
   list_streams, list_streams_unauthenticated, list_streams_unauthorized,
   grant_revoke_own_bucket, grant_revoke_other_bucket, grant_revoke_other_bucket_not_authenticated,
   grant_revoke_own_stream, grant_revoke_other_stream, grant_revoke_other_stream_not_authenticated,
   query_inexistent, query_other_inexistent,
   send_query_last, send_patch_query_last,
   send_other, patch_other,
   send_other_not_authenticated, patch_other_not_authenticated]
end

fn log case Value: ct.pal("~p~n", [Value]) end

fn inexistent_key case:
  list_to_binary(io_lib.format("_this_thing_should_not_exist_~p", [erlang.phash2(now())]))
end

fn query_stream case Config, Bucket, Stream, Limit:
  Conn = conn(Config)
  ioriofn.query(Conn, Bucket, Stream, Limit)
end

fn query_stream case Config, Bucket, Stream:
  Conn = conn(Config)
  ioriofn.query(Conn, Bucket, Stream)
end

fn patch_my_stream case Config, Stream, Data:
  Conn = conn(Config)
  ioriofn.send_patch(Conn, '_user_mariano', Stream, Data)
end

fn send_my_stream case Config, Stream, Data:
  Conn = conn(Config)
  ioriofn.send(Conn, '_user_mariano', Stream, Data)
end

fn query_my_stream case Config, Stream:
  query_stream(Config, '_user_mariano', Stream)
end

fn query_my_stream case Config, Stream, Limit:
  query_stream(Config, '_user_mariano', Stream, Limit)
end

fn new_connection case:
  Host = "localhost"
  Port = 8080
  ioriofn.new(Host, Port)
end

fn new_login case Username, Password:
  (ok, C1) = new_connection()
  ioriofn.login(C1, Username, Password)
end


fn init_per_suite @public case Config:
  application.ensure_all_started(ioriofn)
  Config
end

fn init_per_testcase @public case _Test, Config:
  (ok, Conn) = new_login('mariano', 'secret')
  [(conn, Conn) :: Config]
end

fn with_connection case Fn:
  (ok, C1) = new_connection()
  try
    Fn(C1)
  catch case T, E:
    ct.pal("error: ~p:~p~n", [T, E])
    throw((error, (T, E)))
  after
    ioriofn.close(C1)
  end
end

fn with_login case Fn, Username, Password:
  (ok, C1) = new_login(Username, Password)
  try
    Fn(C1)
  catch case T, E:
    ct.pal("error: ~p:~p~n", [T, E])
    throw((error, (T, E)))
  after
    ioriofn.close(C1)
  end
end

fn end_per_testcase @public case _Test, Config:
  Conn = conn(Config)
  ioriofn.close(Conn)
  Config
end

fn conn case Config:
  proplists.get_value(conn, Config)
end

fn ping_not_authenticated @public case _Config:
  with_connection() <- case Conn:
    (ok, {'pong' = _}) = ioriofn.ping(Conn)
  end
end

fn ping @public case Config:
  Conn = conn(Config)
  (ok, {'pong' = _}) = ioriofn.ping(Conn)
end

fn get_session @public case Config:
  Conn = conn(Config)
  (ok, {'username' = 'mariano'}) = ioriofn.get_session(Conn)
end

fn get_session_not_authenticated @public case _Config:
  with_connection() <- case Conn:
    (ok, {'username' = 'anonymous'}) = ioriofn.get_session(Conn)
  end
end

fn list_buckets_unauthenticated @public case _Config:
  with_connection() <- case Conn:
    (error, (unauthorized, {'reason' = 'No Permission',
                            'type' = 'no-perm'})) = ioriofn.list_buckets(Conn)
  end
end

fn list_buckets_unauthorized @public case Config:
  Conn = conn(Config)
  (error, (unauthorized, {'reason' = 'No Permission',
                          'type' = 'no-perm'})) = ioriofn.list_buckets(Conn)
end

fn list_buckets @public case _Config:
  with_login('admin', 'secret') <<- case Conn:
    (ok, _) = ioriofn.list_buckets(Conn)
  end
end

fn list_streams_unauthenticated @public case _Config:
  with_connection() <- case Conn:
    Response = (error, (unauthorized, {'reason': 'No Permission', 'type': 'no-perm'}))
    Response = ioriofn.list_streams(Conn, '_user_mariano')
  end
end

fn list_streams_unauthorized @public case Config:
  Conn = conn(Config)
  Response = (error, (unauthorized, {'reason': 'No Permission', 'type': 'no-perm'}))
  Response = ioriofn.list_streams(Conn, '_user_admin')
end

fn list_streams @public case Config:
  Conn = conn(Config)
  (ok, {'data' = Streams, 'status' = 'ok'}) = ioriofn.list_streams(Conn, '_user_mariano')
  true = is_list(Streams)
end

fn grant_revoke_own_bucket @public case Config:
  Conn = conn(Config)
  Response = (ok, {'ok': true})
  Response = ioriofn.grant_bucket(Conn, '_user_mariano', 'admin', get)
  Response = ioriofn.grant_bucket(Conn, '_user_mariano', 'admin', put)
  Response = ioriofn.grant_bucket(Conn, '_user_mariano', 'admin', grant)

  Response = ioriofn.revoke_bucket(Conn, '_user_mariano', 'admin', get)
  Response = ioriofn.revoke_bucket(Conn, '_user_mariano', 'admin', put)
  Response = ioriofn.revoke_bucket(Conn, '_user_mariano', 'admin', grant)
end

fn grant_revoke_other_bucket @public case Config:
  Conn = conn(Config)
  Response = (error, (unauthorized, {'reason': 'No Permission', 'type': 'no-perm'}))
  Response = ioriofn.grant_bucket(Conn, 'public', 'admin', get)
  Response = ioriofn.grant_bucket(Conn, 'public', 'admin', put)
  Response = ioriofn.grant_bucket(Conn, 'public', 'admin', grant)
  Response = ioriofn.grant_bucket(Conn, 'public', 'admin', list)

  Response = ioriofn.revoke_bucket(Conn, 'public', 'admin', get)
  Response = ioriofn.revoke_bucket(Conn, 'public', 'admin', put)
  Response = ioriofn.revoke_bucket(Conn, 'public', 'admin', grant)
  Response = ioriofn.revoke_bucket(Conn, 'public', 'admin', list)
end

fn grant_revoke_other_bucket_not_authenticated @public case Config:
  with_connection() <- case Conn:
    Response = (error, (unauthorized, {'reason': 'No Permission', 'type': 'no-perm'}))
    Response = ioriofn.grant_bucket(Conn, 'public', 'admin', get)
    Response = ioriofn.grant_bucket(Conn, 'public', 'admin', put)
    Response = ioriofn.grant_bucket(Conn, 'public', 'admin', grant)
    Response = ioriofn.grant_bucket(Conn, 'public', 'admin', list)

    Response = ioriofn.revoke_bucket(Conn, 'public', 'admin', get)
    Response = ioriofn.revoke_bucket(Conn, 'public', 'admin', put)
    Response = ioriofn.revoke_bucket(Conn, 'public', 'admin', grant)
    Response = ioriofn.revoke_bucket(Conn, 'public', 'admin', list)
  end
end

fn grant_revoke_own_stream @public case Config:
  Conn = conn(Config)
  Response = (ok, {'ok': true})
  Response = ioriofn.grant_stream(Conn, '_user_mariano', 'ct', 'admin', get)
  Response = ioriofn.grant_stream(Conn, '_user_mariano', 'ct', 'admin', put)
  Response = ioriofn.grant_stream(Conn, '_user_mariano', 'ct', 'admin', grant)

  Response = ioriofn.revoke_stream(Conn, '_user_mariano', 'ct', 'admin', get)
  Response = ioriofn.revoke_stream(Conn, '_user_mariano', 'ct', 'admin', put)
  Response = ioriofn.revoke_stream(Conn, '_user_mariano', 'ct', 'admin', grant)
end

fn grant_revoke_other_stream @public case Config:
  Conn = conn(Config)
  Response = (error, (unauthorized, {'reason': 'No Permission', 'type': 'no-perm'}))
  Response = ioriofn.grant_stream(Conn, 'public', 'ct', 'admin', get)
  Response = ioriofn.grant_stream(Conn, 'public', 'ct', 'admin', put)
  Response = ioriofn.grant_stream(Conn, 'public', 'ct', 'admin', grant)

  Response = ioriofn.revoke_stream(Conn, 'public', 'ct', 'admin', get)
  Response = ioriofn.revoke_stream(Conn, 'public', 'ct', 'admin', put)
  Response = ioriofn.revoke_stream(Conn, 'public', 'ct', 'admin', grant)
end

fn grant_revoke_other_stream_not_authenticated @public case Config:
  with_connection() <- case Conn:
    Response = (error, (unauthorized, {'reason': 'No Permission', 'type': 'no-perm'}))
    Response = ioriofn.grant_stream(Conn, 'public', 'ct', 'admin', get)
    Response = ioriofn.grant_stream(Conn, 'public', 'ct', 'admin', put)
    Response = ioriofn.grant_stream(Conn, 'public', 'ct', 'admin', grant)

    Response = ioriofn.revoke_stream(Conn, 'public', 'ct', 'admin', get)
    Response = ioriofn.revoke_stream(Conn, 'public', 'ct', 'admin', put)
    Response = ioriofn.revoke_stream(Conn, 'public', 'ct', 'admin', grant)
  end
end

fn query_inexistent @public case Config:
  (ok, []) = query_my_stream(Config, inexistent_key())
  (ok, []) = query_my_stream(Config, inexistent_key(), 0)
  (ok, []) = query_my_stream(Config, inexistent_key(), 10)
  (ok, []) = query_my_stream(Config, inexistent_key(), 100)
end

fn query_other_inexistent @public case Config:
  Response = (error, (unauthorized, {'reason': 'No Permission', 'type': 'no-perm'}))
  Response = query_stream(Config, '_user_admin', inexistent_key())
end

fn send_query_last @public case Config:
  Data = {'name': 'bob', 'id': erlang.phash2(now())}
  Stream = '_test_ct_'
  (ok, {'meta' = {'id' = Id, 't' = T}}) = send_my_stream(Config, Stream, Data)
  Response = (ok, [{'meta': {'id': Id, 't': T}, 'data': Data}])
  Response = query_my_stream(Config, Stream)
end

fn send_patch_query_last @public case Config:
  Id = erlang.phash2(now())
  Data = {'name': 'bob', 'id': Id}
  Patch = [{'op': 'add', 'path': '/name', 'value': 'plankton'}]
  PatchedData = {'name': 'plankton', 'id': Id}
  Stream = '_test_ct_'

  (ok, {'meta' = {'id' = SendId, 't' = SendT}}) = send_my_stream(Config, Stream, Data)
  (ok, {'meta' = {'id' = PatchId, 't' = PatchT}}) = patch_my_stream(Config, Stream, Patch)

  SendResponse = {'meta': {'id': SendId, 't': SendT}, 'data': Data}
  PatchResponse = {'meta': {'id': PatchId, 't': PatchT}, 'data': PatchedData}
  ExpectedResponse = (ok, [SendResponse, PatchResponse])

  Response = query_my_stream(Config, Stream, 2)

  ExpectedResponse = Response
end

fn send_other_not_authenticated @public case Config:
  with_connection() <- case Conn:
    Id = erlang.phash2(now())
    Data = {'name': 'bob', 'id': Id}
    Response = (error, (unauthorized, {'reason': 'No Permission', 'type': 'no-perm'}))
    Response = ioriofn.send(Conn, 'public', '_test_ct_', Data)
  end
end

fn patch_other_not_authenticated @public case Config:
  with_connection() <- case Conn:
    Patch = [{'op': 'add', 'path': '/name', 'value': 'plankton'}]
    Response = (error, (unauthorized, {'reason': 'No Permission', 'type': 'no-perm'}))
    Response = ioriofn.send_patch(Conn, 'public', '_test_ct_', Patch)
  end
end

fn send_other @public case Config:
  Conn = conn(Config)
  Id = erlang.phash2(now())
  Data = {'name': 'bob', 'id': Id}
  Response = (error, (unauthorized, {'reason': 'No Permission', 'type': 'no-perm'}))
  Response = ioriofn.send(Conn, 'public', '_test_ct_', Data)
end

fn patch_other @public case Config:
  Conn = conn(Config)
  Patch = [{'op': 'add', 'path': '/name', 'value': 'plankton'}]
  Response = (error, (unauthorized, {'reason': 'No Permission', 'type': 'no-perm'}))
  Response = ioriofn.send_patch(Conn, 'public', '_test_ct_', Patch)
end
